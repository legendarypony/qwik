load("@npm//mocha:index.bzl", "mocha_test")
load("@npm//@microsoft/api-extractor:index.bzl", "api_extractor")
load("@build_bazel_rules_nodejs//:index.bzl", "generated_file_test", "js_library", "pkg_npm")
load("@npm//@bazel/typescript:index.bzl", "ts_project")
load("@npm//@bazel/rollup:index.bzl", "rollup_bundle")
load("@npm//@bazel/terser:index.bzl", "terser_minified")

package(default_visibility = ["//visibility:public"])

js_library(
    name = "client",
    package_name = "qoot",
    srcs = ["package.json"],
    deps = [
        ":project",
    ],
)

ts_project(
    name = "project",
    srcs = glob(
        ["**/*.ts"],
        exclude = [
            "qootloader.ts",
            "**/*.unit.ts",
            "CONFIG.ts",
        ],
    ),
    declaration = True,
    tsconfig = "//:tsconfig",
    # TODO: https://bazelbuild.github.io/rules_nodejs/TypeScript.html#ts_project-supports_workers
)

filegroup(
    name = "project.d.ts",
    srcs = [":project"],
    output_group = "types",
)

ts_project(
    name = "CONFIG",
    srcs = ["CONFIG.ts"],
    declaration = True,
    tsconfig = "//:tsconfig",
    deps = [":client"],
)

rollup_bundle(
    name = "qoot",
    config_file = "//:rollup.config.js",
    entry_point = "index.js",
    deps = [":client"],
)

terser_minified(
    name = "qoot.min",
    src = "qoot.js",
    config_file = "//:terser.config.json",
    sourcemap = False,
    terser_bin = "@npm//terser/bin:terser",
)

ts_project(
    name = "qootloader",
    srcs = ["qootloader.ts"],
    declaration = True,
    tsconfig = "//:tsconfig",
)

terser_minified(
    name = "qootloader.min",
    src = "qootloader.js",
    config_file = "//:terser.config.json",
    sourcemap = False,
    terser_bin = "@npm//terser/bin:terser",
)

ts_project(
    name = "unit_lib",
    srcs = glob([
        "**/*.unit.ts",
        "**/*.unit.tsx",
    ]),
    declaration = True,
    tsconfig = "//:tsconfig",
    deps = [
        ":CONFIG",
        "//client/testing",
        "@npm//@types/chai",
        "@npm//@types/mocha",
    ],
)

mocha_test(
    name = "unit",
    args = ["**/*.unit.js"],
    data = [
        ":unit_lib",
        "//client/testing",
        "@npm//chai",
        "@npm//domino",
    ],
    tags = ["unit"],
)

api_extractor(
    name = "public_api_extractor",
    outs = [
        "qoot.d.ts",
        ":qoot.api.md",
    ],
    args = [
        "run",
        "--verbose",
        "--local",
        "--config",
        "client/api-extractor.json",
    ],
    data = [
        "api-extractor.json",
        ":project",
        "//:package.json",
        "//:tsconfig.base.json",
        "//:tsconfig.bazel.json",
    ],
)

generated_file_test(
    name = "publicAPI.md",
    src = ":qoot.api.gold.md",
    generated = ":qoot.api.md",
    tags = ["unit"],
)

genrule(
    name = "LICENSE.copy",
    srcs = ["//:LICENSE"],
    outs = ["LICENSE"],
    cmd = "cp $< $@",
    output_to_bindir = 1,
)

pkg_npm(
    name = "qoot_pkg",
    srcs = [
        ":package.json",
    ],
    substitutions = {"//internal/": "//"},
    deps = [
        ":LICENSE",
        ":qoot",
        ":qoot.d.ts",
        ":qoot.min",
        ":qootloader",
        ":qootloader.min",
        "//client/cjs:package.json",
        "//client/cjs:qoot",
    ],
)
